import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        //noinspection UseTomlInstead,AndroidGradlePluginVersion
        classpath 'com.android.tools.build:gradle:8.13.0'
        //noinspection UseTomlInstead
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.21'
        //noinspection UseTomlInstead
        classpath 'org.jreleaser:jreleaser-gradle-plugin:1.19.0'
    }
}

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.kernelflux.maven.publish' version '1.0.6'
}

// 发布配置
// 注意：底层 PAG 库使用 -lib 后缀，避免与上层 aniflux-pag 模块命名冲突
// -lib 表示底层库（包含 Java/Kotlin 封装和 native 代码），区别于上层集成模块
// 版本号从根项目的 ext.anifluxLibVersion 统一管理
ext {
    publishArtifactId = "aniflux-pag-lib"
    publishVersion = rootProject.ext.has("anifluxLibVersion") 
        ? rootProject.ext.get("anifluxLibVersion") 
        : "0.0.1"  // 降级方案
    publishBundleName = "aniflux_pag_lib_bundle_v${publishVersion}"
}


// Try to access version catalog, fallback to hardcoded values
def getVersionCatalog() {
    try {
        return rootProject.extensions.getByName("libs")
    } catch (Exception e) {
        return null
    }
}

def libs = getVersionCatalog()


android {
    compileSdkVersion libs ? libs.versions.compileSdk.get().toInteger() : 36
    ndkVersion "28.0.13004108"

    defaultConfig {
        minSdkVersion libs ? libs.versions.minSdk.get().toInteger() : 21
        targetSdkVersion libs ? libs.versions.targetSdk.get().toInteger() : 36

        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static",
                        "-DPAG_USE_LIBAVC=OFF",
                        "-DPAG_USE_FFAVC=ON"
            }
        }

        ndk {
            if (project.hasProperty("arm64-only")) {
                abiFilters.add("arm64-v8a")
            } else {
                abiFilters.add("armeabi-v7a")
                abiFilters.add("arm64-v8a")
                // x86_64 removed temporarily due to build issues
                // Uncomment if needed for emulator support
                // abiFilters.add("x86_64")
            }
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // consumerProguardFiles 会将规则打包进 aar，供使用该库的应用使用
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path '../../CMakeLists.txt'
            version = "3.22.1"
        }
    }
    namespace 'com.kernelflux.pag'

    publishing {
        singleVariant("release") {}
    }


}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection UseTomlInstead
    api("androidx.exifinterface:exifinterface:1.4.1")
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.source
    exclude "**/R.class"
    exclude "**/BuildConfig.class"
}

// 应用通用发布配置
apply from: rootProject.file('gradle/maven-publish.gradle')


project.afterEvaluate {
    mergeReleaseNativeLibs.doLast {
        delete {
            delete project.getBuildDir().path + '/intermediates/merged_native_libs/release/mergeReleaseNativeLibs/out/lib/armeabi'
        }
        copy {
            from project.getBuildDir().path + '/intermediates/merged_native_libs/release/mergeReleaseNativeLibs/out/lib/armeabi-v7a'
            into project.getBuildDir().path + '/intermediates/merged_native_libs/release/mergeReleaseNativeLibs/out/lib/armeabi'
            include '*.so'
        }
    }
    stripReleaseDebugSymbols.doLast {
        delete {
            delete project.getBuildDir().path + '/intermediates/stripped_native_libs/release/stripReleaseDebugSymbols/out/lib/armeabi'
        }
        copy {
            from project.getBuildDir().path + '/intermediates/stripped_native_libs/release/stripReleaseDebugSymbols/out/lib/armeabi-v7a'
            into project.getBuildDir().path + '/intermediates/stripped_native_libs/release/stripReleaseDebugSymbols/out/lib/armeabi'
            include '*.so'
        }
    }
    // 此处为了方便debug，保留debug版本armeabi的拷贝，方便源码接入时候的调试
    mergeDebugNativeLibs.doLast {
        delete {
            delete project.getBuildDir().path + '/intermediates/merged_native_libs/debug/mergeDebugNativeLibs/out/lib/armeabi'
        }
        copy {
            from project.getBuildDir().path + '/intermediates/merged_native_libs/debug/mergeDebugNativeLibs/out/lib/armeabi-v7a'
            into project.getBuildDir().path + '/intermediates/merged_native_libs/debug/mergeDebugNativeLibs/out/lib/armeabi'
            include '*.so'
        }
    }
    stripDebugDebugSymbols.doLast {
        delete {
            delete project.getBuildDir().path + '/intermediates/stripped_native_libs/debug/stripDebugDebugSymbols/out/lib/armeabi'
        }
        copy {
            from project.getBuildDir().path + '/intermediates/stripped_native_libs/debug/stripDebugDebugSymbols/out/lib/armeabi-v7a'
            into project.getBuildDir().path + '/intermediates/stripped_native_libs/debug/stripDebugDebugSymbols/out/lib/armeabi'
            include '*.so'
        }
    }
}