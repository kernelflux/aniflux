plugins {
    id 'com.android.library'
    id 'com.kernelflux.maven.publish' version '1.0.8'
}

// 发布配置
// 注意：底层 Lottie 库使用 -lib 后缀，避免与上层 aniflux-lottie 模块命名冲突
// 版本号从根项目的 ext.anifluxLibVersion 统一管理
ext {
    publishArtifactId = "aniflux-lottie-lib"
    publishVersion = rootProject.ext.has("anifluxLibVersion") 
        ? rootProject.ext.get("anifluxLibVersion") 
        : "0.0.1"  // 降级方案
    publishBundleName = "aniflux_lottie_lib_bundle_v${publishVersion}"
}

android {
    namespace 'com.kernelflux.lottie'
    resourcePrefix 'lottie_'
    compileSdkVersion libs.versions.compileSdk.get().toInteger()
    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {}
    }
}

dependencies {
    api libs.androidx.appcompat
    // Do not upgrade to 2.0 because it will bring in Kotlin as a transitive dependency.
    //noinspection GradleDependency
    implementation 'com.squareup.okio:okio:3.9.1'
}



// 应用通用发布配置
apply from: rootProject.file('gradle/maven-publish.gradle')
