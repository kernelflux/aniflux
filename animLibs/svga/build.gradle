import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.kernelflux.maven.publish' version '1.0.8'
}

// 发布配置
// 注意：底层 SVGA 库使用 -lib 后缀，避免与上层 aniflux-svga 模块命名冲突
// 版本号从根项目的 ext.anifluxLibVersion 统一管理
ext {
    publishArtifactId = "aniflux-svga-lib"
    publishVersion = rootProject.ext.has("anifluxLibVersion") 
        ? rootProject.ext.get("anifluxLibVersion") 
        : "0.0.1"  // 降级方案
    publishBundleName = "aniflux_svga_lib_bundle_v${publishVersion}"
}


android {
    namespace = "com.kernelflux.svga"
    compileSdkVersion libs.versions.compileSdk.get().toInteger()
    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        kotlinOptions.freeCompilerArgs += ['-module-name', "com.kernelflux.svgaplayer"]
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    publishing {
        singleVariant("release") {}
    }
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "11"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation libs.wire.runtime
    implementation libs.kotlin.stdlib.jdk7
}

// 应用通用发布配置
apply from: rootProject.file('gradle/maven-publish.gradle')
